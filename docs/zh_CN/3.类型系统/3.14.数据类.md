## 数据类

**数据类类型（Data Type）** 是一种特殊的类型声明形式，它使用`data`关键字修饰组件声明表示。

在数据类组件中所有的外部可见变量受严格限制，其声明只能以参数化变量的形式存在于初始化函数参数中，不允许定义任何**非private**的**非纯属性**，如下是一个简单的数据类组件的声明：

```ecs
data comp DataType(
  val id: Int,
  var name: String,
)
```

同时，数据类组件包含的组件必须也是数据类类型，在不明确调用包含的数据类初始化函数时，此组件会按照其包含组件的扁平化序列依次拼接所有组件的初始化参数，以作为此类型的初始化函数参数，例如：

```ecs
data comp Sub(var age: Int): DataType

new Sub(18, 1, "Wilson")
```

多数情况下，对数据类类型使用与普通类型在语法上没有区别，你可以按照访问一般类型的方式去访问数据类类型。

**元组（Tuple）**，**结构（Struct）** 本身就是数据类类型。

### 仅栈与泄漏

数据类类型最大的特点就是它在函数中期望不以对象的形式参与计算，其在函数内如果没有发生泄漏，则始终不会实例化任何此类型的新对象。

对数据类类型的使用会在编译时被展开为若干个与成员相对应的局部变量，例如如下函数：

```ecs
fun foo1(){
  val dat = new DataType(1, "Joe")
  
  println(dat.name)
  dat.name = "Wilson"
  println("${dat.name}:${dat.id}")
}
```

上述程序将被分析为如下等价代码：

```ecs
fun foo1(){
  val _dat_id = 1
  var _dat_name = "Joe"
  
  println(_dat_name)
  _dat_name = "Wilson"
  println("$_dat_name:$_dat_id")
}
```

可以看到`new DataType(...)`这一表达式被平展为了局部变量赋值，在函数全文中始终没有产生新的`DataType`对象，此时这个数据对象就是仅工作在栈上的。

但是如果数据对象以任何形式被传递到了函数之外的非数据标记位置，那么就认为这个数据对象发生了**栈泄漏（Stack Leak）**。

以下任意一项行为都会导致数据类发生泄漏：

- 将数据对象作为函数的返回值返回，且这个函数没有被标记为`data`
- 调用函数时，将数据对象作为参数传递，同时这个参数没有被标记为`data`
- 将数据对象传递给任意一个没有被标记为`data`的外部变量及属性
- 主动的使用特殊的`*new`关键字实例化数据类
- 对数据对象使用`*new`表达式强制实例化为对象

当数据对象发生泄漏，在泄漏处就会产生此数据对象的新实例，且自发生泄漏的点位开始，之后所有对数据对象的访问均会以常规类型的方式对这个新实例进行。

> 一般而言，约定数据对象泄漏应当主动使用`*new`关键字进行，意外的泄漏应当被报告。

例如如下所示的代码：

```ecs
fun foo2(){
  val dat = new DataType(1, "Joe")
  
  println(dat.id)
  foo3(dat)
  println(dat.name)
}

fun foo3(dat: DataType){
  println(${dat.name}:${dat.id})
}
```

上述代码将会被解析为如下等价代码：

```ecs
fun foo2(){
  val _dat_id = 1
  var _dat_name = "Joe"
  
  println(_dat_id)
  
  val d = new DataType()
  foo3(d)
  println(d.name)
}
```

### 数据化

有时候，我们会需要在函数之间传递数据对象，同时又不希望数据对象被实例化。此时需要将接收数据对象的位置作数据化标记。

`data`关键字可以被用在函数的参数上，函数上，还有属性上，以标记这些位置是期望接收的数据对象的区域，当数据对象被传递给被数据化的域时，就不会造成栈泄漏。

注意，`data`修饰的目标记录类型都必须是数据类类型。

#### 函数参数数据化

如上文所示的`foo3()`，当数据类被传递给参数时会导致栈泄漏，如果要令此处不发生泄漏，则需要将这个参数设置为`data`，如下所示：

```ecs
fun foo3(data dat: DataType){
  println(${dat.name}:${dat.id})
  dat.name = "None"
}
```

被修饰为`data`的参数会在编译时按其包含的可见变量进行展开，平展到参数表中。对于上述的这段程序，它将会被解析为如下等价代码：

```ecs
fun foo2(){
  val _dat_id = 1
  var _dat_name = "Joe"
  
  println(_dat_id)
  
  foo3(_dat_id, _dat_name, { _dat_name = it })
  println(_dat_name)
}

fun foo3(id: Int, name: String, nameSetter: (String) -> Unit){
  val _dat_id = id
  var _dat_name = name
  
  println($_dat_name:$_dat_id)
  _dat_name = "None"
  nameSetter(_dat_name)
}
```

如上所示，对可变量除传递其值之外，还会向参数中传递一个用于修改原变量值的闭包函数。

另外，由于`data`修饰参数可能会导致参数变得异常的长，编译器仍然会保留平展前的函数签名供多语言交互时使用，所以事实上`foo3`还会创建一个新的重载函数：

```ecs
fun foo3(dat: DataType){
  foo3(dat.id, dat.name, { dat.name = it })
}
```

#### 函数返回值数据化

使用`data`修饰函数时，函数的返回值将被数据化，数据化的函数可以避免返回的数据对象发生泄漏。

例如如下函数返回了一个数据对象，但是它并不会使这个数据对象发生泄漏：

```ecs
data fun foo4(): DataType{
  return new DataType(2, "Alen")
}

fun foo5(){
  val dat = foo4()
  
  println(dat.name)
}
```

相比于数据化参数而言，数据化的函数返回值的工作方式会比较复杂。

具体来说，对一个数据化类型，编译器会创建与此类型关联的数据单例，并一一保存此类型当中的所有可见变量的线程局部变量，同时生成从外部访问这些局部变量的纯属性。

当一个函数以数据化的形式返回一个数据对象时，实际上是将数据对象的信息存储到这个单例中后，返回这个单例。

对于上述的例子，其等效代码如下所示：

```ecs
data singleton DataType$Member{
  private val _id = new ThreadLocal<Int>()
  private val _name = new ThreadLocal<String>()
  
  val id: Int{
    get() = _id.get()
  }
  var name: String{
    get() = _name.get()
    set(value) = _name.set(value)
  }
  
  fun toHeap(): DataType = new DataType(id, name)
}

fun foo4(): DataType$Member{
  DataType$Result.id = 2
  DataType$Result.name = "Alen"
  return DataType$Result
}

fun foo5(){
  val res = foo4()
  val _dat_id = res.id
  var _dat_name = res.name
  
  println(_dat_name)
}
```

#### 变量数据化

变量的数据化一定程度上和C系编程语言中的**结构体**十分类似，当一个变量被标记为`data`时，经过编译，它会在其所在的层级中按其数据类的类型的成员平展为一系列成员变量。

当函数中的数据对象被传递给一个标记为`data`的外部变量时，数据对象不会发生泄漏，而是会将数据对象中的变量信息传递给接收的`data`变量平展的所有变量。

例如，对于如下所示的例子，该程序全程也没有创建任何新的`DataType`对象：

```ecs
data var dat: DataType = new DataType(3, "None")

fun foo6(){
  println(dat.id)
  dat = new DataType(4, "Sam")
  println(dat.name)
  dat.name = "Alice"
  println(dat.name)
}
```

它经过编译后的等效代码如下：

```ecs
@AttacheMember(DataType::id) var _dat_id = 3
@AttacheMember(DataType::name) var _dat_name = "None"

fun foo6(){
  println(_dat_id)
  _dat_id = 4
  _dat_name = "Sam"
  
  println(_dat_name)
  _dat_name = "Alice"
  println(_dat_name)
}
```

> 上述所有解释产生的等效代码均不是实际编译的结果，仅作说明用途。

### 数据数组

如果将数据对象保存数组时，这个数组将会被展开为多个数组分别存储数组元素的信息。

展开的规则和普通的数据类规则相同，只是元素的成员会被分别装入数组中，例如，对于如下函数中出现的数组：

```ecs
fun foo7(){
  val arr = new [DataType*10]{ i -> new DataType(i, "$i") }
  
  print(arr[5].name)
}
```

在经过编译之后，这个数组会被展开成其成员组成的数组，下面是编译后的等效代码：

```ecs
fun foo7(){
  val _arr_id = new [Int*10]{ i -> i }
  val _arr_name = new [String*10]{ i -> "$i" }
  
  print(_arr_name[5])
}
```

对于多维数组也是相同的：

```ecs
fun foo7(){
  val arr = new [[DataType*10]*10]{ i, j -> new DataType(i*j, "$i$j") }
  
  print(arr[5][5].name)
}
```

上述代码编译后的等效代码如下：

```ecs
fun foo7(){
  val _arr_id = new [[Int*10]*10]{ i, j -> i*j }
  val _arr_name = new [[String*10]*10]{ i, j -> "$i$j" }
  
  print(_arr_name[5][5])
}
```
