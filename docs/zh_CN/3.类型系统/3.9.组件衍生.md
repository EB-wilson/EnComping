## 组件衍生

组件结构可使用`override`关键字声明覆盖组件，用于构造在一个基组件上的行为变体，而这个新的组件即原组件的**衍生组件（Derived Component）**，其语法如下：

```ecs
override OverrideComp: OriginComp { body }
```

`{body}`是覆盖组件的类型体，**在此体中不可声明任何新的在此组件之外可见的成员**，它只能声明在其覆盖的组件中已经声明的成员的重写，这不包含被覆盖成员所包含的组件列表。

你可以像使用常规的组件一样使用和包含衍生组件，它的成员会与被覆盖的组件成员完全相同，但他们的实际行为不同。

覆盖组件用于创建组件变体，用于重写其覆盖的组件中的可衍生成员，进行衍生的成员需要使用`override`修饰，而衍生成员可通过`super`关键字来访问被覆盖的成员，如下例所示：

```ecs
comp Comp1{
  fun foo(){
    println("hello world")
  }
}

override Comp2: Comp1{
  override fun foo(){
    super.foo()
    println("hello EnComping")
  }
}

new Comp1().foo() // hello world
new Comp2().foo() // hello world
                     hello EnComping
```

同样的，属性也是可以被覆盖的，如下例所示：

```ecs
comp Comp1{
  var a: Int = 1
  val b: Int = 2
}

override Comp2: Comp1{
  override var a: Int{
    default = 0
    
    get() = default++
    set(value) = default = value
  }
  
  override val b: Int{
    get() = super.b + 1
  }
}

val c = new Comp2()
c.a // 0
c.a // 1
c.a // 2

new Comp2().a // 3
```

特别注意，与常见的面向对象语言并不相同，ECP中的覆盖组件与原组件是等位的，即衍生相同接口下的不同行为，而非对被覆盖类型的扩展，这也意味着在衍生组件中不可定义任非`private`或`protected`的成员。

### 可衍生成员

可衍生成员是在组件当中可以被覆盖的函数或者属性，具有有如下特征：

- 是成员函数或者成员属性
- 不是初始化函数
- 非`private`
- 成员变量非`const`
- 非`final`

> `final`修饰符与属性值的可变性无关，它只用于描述此函数或者属性是一个不可衍生的成员，使得此成员无法被覆盖。

当`final`修饰符被用于组件上时，这整个组件都无法被用于覆盖变体组件，但并不会影响组件的包含。

如下代码是非法的：

```ecs
final comp Comp{...}

override OverComp: Comp{...}
```

### 使用衍生组件进行动态替换

在上一章中提到，组件在运行时是可以被动态替换的，衍生组件同样可以被直接提供给`<-`表达式，例如如下所示的代码：

```ecs
comp Comp(val id: Int){
  fun foo(){
    println("hello world")
  }
}

override Comp2(id: Int): Comp(id){
  override fun foo(){
    println("hello EnComping")
  }
}

val obj = new Comp(1)
obj.foo()       // hello world
println(obj.id) // 1
obj:Comp <- Comp2(10)
obj.foo()       // hello EnComping
println(obj.id) // 10
```

请注意，选择组件使用的命名空间并不会因为组件替换而发生改变，选择器命名空间始终都会保持在声明时的类型名称或者别名，如下所示：

```ecs
val obj1 = new Comp(1)
obj1:Comp <- Comp2(10)
obj1:Comp <- Comp2(5)

val obj2 = new Comp2(5)
obj2:Comp2 <- Comp(2)
obj2:Comp2 <- Comp2(6)
```

同样的，如果需要将替换扩展到整个组件的包含结构，也需要使用`*`去修饰提供用于替换的组件：

```ecs
comp Comp1(val id: Int)
comp Comp2(id: Int): Comp1(id){
  fun foo(){
    println(id)
  }
}

override OverComp(id: Int): Comp2(id){
  override fun foo(){
    println("over: $id")
  }
}

val obj = new Comp2(1)
obj.foo() // 1
obj:Comp2 <- OverComp(10)
obj.foo() // over: 1
obj:Comp2 <- *OverComp(10)
obj.foo() // over: 10
```
