## 可空类型

ECP是空值严格的语言，类型会包含可空或者非空信息，这会决定此变量/参数的数据是否可以为`null`。

默认情况下，类型均是非空的：

```ecs
var str = "str"
var num = 1
var bool = true
```

且下述代码是非法的：

```ecs
str = null
```

而可空类型只需要在类型的后方附加一个`?`，如下所示：

```ecs
var str: String? = null
var num: Int? = null
var bool: Boolean? = null
```

可空类型与非空类型是两个不同的类型，且只能从非空到可空单向赋值，例如：

```ecs
var string1: String? = "a"
var string2: String = "b"

string1 = string2 // 合法
string2 = string1 // 非法，类型不可分配
```

用在参数上也是一样的：

```ecs
var string1: String? = "a"
var string2: String = "b"

fun foo(str: String){
  println(str)
}

foo(string2) // 合法
foo(string1) // 非法，类型不可分配
```

### 空值可选操作

当访问可空类型的变量时，或者将可空变量的值赋给非空类型，则必须对此可空变量执行**可选（Optional）** 操作，具体来说这是一些符号系列，根据变量是否为空来做出不同的响应：

- `!!` - 强制非空转换  
  使用强制非空时，表示在某个调用位置的可空函数一定不为空，可将此可空变量直接转换为非空变量，但是如果此变量的值在这里是`null`，则会抛出错误并终止程序。
  ```ecs
  var str: String? = "abc"
  
  println(str!!.length)   // 输出3
  ```

- `?.` - 空值检查访问  
  空值检查会在对可空变量访问成员时，检查此变量是否为空，当变量不为空时才会继续进行之后对成员的访问，否则此表达式会直接结束并返回`null`。
  特别的，如果此表达式被用于布尔表达式，那么它在左值为空时会返回`false`，而如果此表达式被用于条件分支或者流程控制，那么在因此值不为空而进入的块中此变量将被认为是非空的，即无需再进行非空检查：
  ```ecs
  fun foo(str: String?){
    if(str?.isEmpty()) return
  
    println(str)
  }
  ```
  > 实际上，使用`if(str != null)`也可以达到相同的效果，在条件分支或者流程控制之下因非空检查而进入的块中，对此变量即不需要再进行空值检查。

- `?:` - 空值备选
  空值备选符被用于在可空变量的值为空值时，使用备选的表达式产生一个非空的同类型值作为替代：
  ```ecs
  fun foo(str: String?){
    println(str?: "str is null!")
  }
  
  foo("hello world") // 输出 hello world
  foo(null) // 输出 str is null!
  ```

### 可空类型转换

在ECP中使用关键字`as`进行强制类型转换，但是强制类型转换会在类型转换失败时抛出错误，如果需要在此强制转换失败时产出`null`而非报错，则可以使用安全的可空类型转换符号`?as`：

```ecs
fun foo(str: Any){
  val pr: String? = str ?as String
  println(pr?: "str is not a String")
}

foo("hello world") // 输出 hello world
foo(1)             // 输出 str is not a String
```

### NullPointerException

由于在ECP内所有值都是空值安全的，可能产生`NullPointerException`的情况只有以下几种可能：

- 使用`!!`强制转换可空类型为非空，但是此值实际为`null`
- 主动抛出`throw new NullPointerException()`
- 意外的在初始化函数中访问尚未被赋值的成员属性
- 与Java互操作时来自Java中的抛出的空值异常
