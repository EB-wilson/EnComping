## 元组

**元组（Tuple）** 是储存一系列**不可变**数据的简单数据容器，提供了一系列相关的语法以支持更直观的多值传递。

在ECP中，元组类型使用`(Value Types)`表示，其中`Value Types`为类型列表，其表示在此元组中的若干个数据的对应类型，而创建一个元组只需要使用括弧将若干数据扩起来，并用逗号分隔：

```ecs
val t1: (Int, String, Boolean) = (1, "hello", true)
val t2: (Float, String?, Boolean, Int) = (2f, null, false, 2)
```

上述变量类型可缺省，从元组声明进行推断：

```ecs
val t3 = (3, "world", false)
val t4 = (4f, null ?as String, true, 4)
```

元组也是ECP实现多重返回值的方式：

```ecs
comp Position{
  var x = 0
  var y = 0

  fun point(): (Int, Int){
    return (x, y)
  }
}

val p = Position()

var x = 0
var y = 0

(x, y) = p.point()
```

### 访问元组

从元组中提取数据可分为两种：
- 当使用赋值符号`=`从元组提取数据时，赋值的左值需要是以括弧`(...)`包围起来的与元组数据量对等的若干个变量：
   
  ```ecs
  var a = 0
  var b = ""
  var c = false
    
  (a, b, c) = t1
    
  println(a)  // 1
  println(b)  // "hello"
  println(c)  // true
  ```
    
  如果需要获取的接收者需要缺省，可以使用下划线`_`作占位符：
    
  ```ecs
  var str = ""
    
  (_, str, _) = t1
    
  println(str)  // "hello"
  ```
    
  在此语法基础上，可以轻松的交换变量值：
    
  ```ecs
  var a = 0
  var b = 1
  var c = 2
    
  (a, b) = (b, a)
  (a, b, c) = (b, c, a)
  ```

- 而另一种方式用于在表达式中直接提取元组中的值，通过对元组使用`._<order>`获取在元组中的第`order`个元素值：

  ```ecs
  val t = ("hello", "world")
  
  println(t._0)  // "hello"
  println(t._1)  // "world"
  ```
  
  元组不可以通过`[index]`或者`.get(index)`动态访问成员，在上述调用中的序号可用范围是明确的，越界的`order`在语法检查阶段就会检查并报错。

### 元组切片

使用对元组使用`(slice)`可以提取出此元组中的若干个元素构造一个新的元组，其中`slice`被称为提取器，它用于定义如何对元组进行切片。

一个简单的切片操作如下：

```ecs
val tup = (1, "2", 3f, 4.0, true)
val slice = tup(_, *, _, *, *)

println(slice)  // ("2", 4.0, true)
```

上述提取器中出现的两个占位符`_`和`*`分别表示忽略一个位置和提取一个位置，此外，每一个占位符后均可附加一个整数表示这个占位符被重复输入多少次，例如对上述代码中出现的提取器，可写作：

```ecs
val slice = tup(_, *, _, *2)
```

两种写法效果完全一致。

此外元组切片也可以使用区间作为提取器，但是此区间必须是常量整数区间：

```ecs
val tup = (1, "2", 3f, 4.0, true)

println(tup(1..3)) // ("2", 3f, 4.0)
println(tup(0..2)) // (1, "2", 3f)
```
