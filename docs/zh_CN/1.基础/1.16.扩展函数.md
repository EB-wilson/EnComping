## 扩展成员

**扩展成员（Extend Member）** 是一种静态解析的功能，它可以提供外置于目标扩展类型的函数和属性，当你使用这些函数与属性时，看起来就像是这些函数与属性就存在于目标扩展类型中一样。

扩展成员的声明即在函数/变量声明的名称标识符前加上附加的类型名称，此时此函数/变量会将此类型作为接收者类型：

```ecs
fun Int.pow(Int n){
  Int res = this
  for(i in 1..n-1){
    res *= this
  }
  return res
}

val Vector2.len{
  get() = Math.sqrt(x*x + y*y)
}

10.pow(3)            // 1000
new Vector2(3,4).len // 5
```

### 静态解析

扩展成员是静态解析的，即成员是外置于接收者的类型，在调用扩展成员时，具体被调用的成员是在编译时即被确定了。

所以扩展成员并没有为接收者创建新的成员，仅仅是可以通过该类型的变量用点表达式去调用这个外置的成员。

### 扩展属性

如前文所述，扩展属性是静态解析的，这意味着扩展属性必须是**纯属性**，不允许给定`default`声明，因为属性外置于类型，在类型中并不能为此扩展提供属性的内存空间。

```ecs
var Vector2D.angle{
  get() = Math.atan2(y, x)
  set(value) {
    x = Math.cos(value)
    y = Math.sin(value)
  }
}
```

而如下语句则是非法的：

```ecs
val Int.increase{
  default = this    // 错误，不允许在扩展属性中声明default
  
  get() = default++
}
```

### 可访问性

依然是因为扩展成员为静态解析，其对扩展的类型中的成员访问会受到限制，只能访问来自扩展的类型中的公有成员，`internal`和`private`成员均无法在扩展成员中访问。

```ecs
comp Comp{
  val a = 10
  internal val b = "Hello world"
  
  fun foo(){
    print()
  }
  
  private fun print(){
    println(b)
  }
}

fun Comp.ext(){
  for(i: Intin 1..10){
    foo()
  }
}
new Comp().ext() // 连续打印10 次 "Hello world"
```

如下代码则非法的：

```ecs
fun Comp.ext1(){
  println(b) // 错误，无法访问 b
  print()    // 错误，无法访问 print() 
}
```

### 作为成员的扩展

扩展成员本身也可以作为组件的成员被声明，此时该扩展函数会具有两个`this`接收器分别接收自身所在的对象和扩展接收的对象，其中，用于接收扩展接收者的`this`接收器被重命名为`this@ReceiverType`，如果在接收者和声明的类型中存在签名相同的成员，隐含`this`访问时将会优先选取此类型中的成员，否则应当采用被重命名后的`this`，如下代码所示：

```ecs
comp Comp{
  fun foo(){
    println("Hello world")
  }
}

comp Ext{
  fun Comp.ext(){
    this@Comp.foo()
  }
  
  fun foo(){
    println("Hello world")
  }
}
```


一般而言，在声明扩展的类型内，用于传递给接收器的对象是扩展的目标类型，如下代码所示：

```ecs
comp Comp{
  fun foo(){
    println("Hello world")
  }
}

comp Ext{
  val str = "extend"

  fun Comp.ext(){
    foo()
    println(str)
  }
  
  fun print(target: Comp){
    target.ext()
  }
}

new Ext().print(new Comp()) // 打印 "Hello world" 和 "extend"
```

而当调用作为成员的扩展函数时，其接收器则会被转换为函数的第一个参数：

```ecs
val obj = new Ext()
obj.ext(new Comp()) // 打印 "Hello world" 和 "extend"
```

成员属性则不可在声明此扩展属性的类型外调用，因为此时扩展接收器将无法被确定。

特别的是，作为成员的扩展成员是可衍生的，它可以被覆盖组件覆盖，衍生，例如对上述例子：

```ecs
override OverExt: Ext{
  override fun Comp.ext(){
    super.ext()
    println("override")
  }
}

new OverExt().print(new Comp()) // "Hello world"
                                // "extend" 
                                // "override"
```

### 扩展成员作用域

扩展函数需要确定作用域后才能使用。

- 通常，当扩展被声明为顶级语句时，在使用`import`导入此扩展成员后即可使用该扩展
- 如果扩展被声明在单例中，则在`import`导入此单例后即可使用单例中的所有扩展成员
- 如果扩展成员是另一个类型的成员，则只能在此类型体内可用，之外的地方此扩展成员将降级为普通的成员函数，请查阅前一节

```ecs
fun Ext.foo(){...} // 顶级语句扩展

singleton Example{
  fun Ext.foo(){...} // 单例成员扩展
}

comp Comp{
  val Ext.attr{ //成员扩展
    get(){...}
  }
}
```
