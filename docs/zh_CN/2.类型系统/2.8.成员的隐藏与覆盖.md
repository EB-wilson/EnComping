## 成员隐藏与覆盖

正常情况下，对对象直接访问其成员时，这些成员大都来自它包含的组件，对成员的调用会被委托给这个成员的来源组件。

但是，有时候在组件当中会有重名的函数或者属性，例如如下这个多层叠加的组件包含结构：

```ecs
comp Comp1{
  var variable = "hello"
  
  fun foo(){
    println("origin")
  }
}

comp Comp2: Comp1{
  fun foo(){
    println(variable)
  }
}

comp Comp3: Comp2{
  var variable = "world"
}
```

上述的几个组件当中有非常多重名的成员声明，每一层包含，重名的非私有成员都会同名覆盖上层的同名成员，这种现象被称为**隐藏（Hide）**。

成员隐藏在ECP中代替了其他语言中常见的**虚拟方法（Virtual Method）**，对组件成员的隐藏覆盖可提供完全一致的效果，且ECP保留了访问被覆盖的成员的能力。

在对这些重名的成员进行调用时，会访问到最靠近组合的层次中的同签名成员，例如在`Comp2`中，`foo()`隐藏了来自`Comp1`的同签名函数，调用会选择最接近组合对象中的`foo()`，即`Comp2`中的此函数：

```ecs
new Comp1().foo() // origin
new Comp2().foo() // hello
```

但是对`new Comp3().foo()`时，可以注意到在`Comp2`中对`variable`的访问是位于包含的组件内部的，然而`Comp3`中的同名属性隐藏了`variable`，当运行此语句将会得到如下结果：

```ecs
new Comp3().foo() // world
```

### 访问被隐藏成员

被隐藏的成员一般来说可以通过指明目标组件来进行访问，就像前一章提到过的那样：

```ecs
ent:Position.move(1, 2)
```

不过，当对被隐藏成员的访问存在于隐藏了此成员的组件中时，例如：

```ecs
comp Position{
  var x = 0
  var y = 0
  
  fun move(dx, dy){
    x += dx
    y += dy
  }
}

comp Motion: Position{
  var velX = 0
  var velY = 0
  
  fun updatePos(){
    move(velX, velY)
  }
}

comp Movable: Motion{
  fun move(dx, dy){
    velX += dx
    velY += dy
    
    this:Position.move(dx, dy)
  }
}
```

上述`Movable`组件隐藏了`Position`中的`move(x, y)`函数，而当我们需要访问被隐藏函数时需要像上面那样编写`this:Position.move(dx, dy)`去重定向函数调用，这显得十分繁琐，为此，我们提供一个关键字`hidden`用于指向最近的被隐藏成员，使用`hidden`关键字改写上述程序：

```ecs
comp Movable: Motion{
  fun move(dx, dy){
    velX += dx
    velY += dy
    
    hidden.move(dx, dy)
  }
}
```

> 请注意，`hidden`关键字访问被隐藏成员时要求最近层次中的同签名被隐藏成员是唯一的，如果有不止一个被隐藏的同签名成员存在于在最近的同一层次中，则因为调用不明确而不允许使用`hidden`。

### 隐藏控制

就像上文所述，成员的隐藏会向之后的程序中传递，但是有时候我们并不希望某个成员被隐藏，或者因为隐藏而改变某个已知成员的行为，有以下几种方式控制隐藏：

- 使用`final`修饰符修饰不可被隐藏的成员  
  当成员被标记为`final`时，对此成员的调用始终都会被定位到它的来源，而不会因为隐藏而发生改变，可认为对`final`成员的访问都是明确指名组件的：

  ```ecs
  comp Comp1{
    final val str = "hello world"  
  
    fun foo(){
      println(str)
    }
  }
  
  comp Comp2: Comp1{
    val str = "override"
  }
  
  new Comp2().foo()  // 输出 "hello world"
  ```

- 使用`private`使不可隐藏的成员作为私有的  
  `private`修饰符产生的实际效果与`final`十分相似，对此成员的访问也是组件明确的，不被隐藏：

  ```ecs
  comp Comp3{
    private val str = "hello world"  
  
    fun foo(){
      println(str)
    }
  }
  
  comp Comp4: Comp3{
    val str = "override"
  }
  
  new Comp2().foo()  // 输出 "hello world"
  ```

- 使用指名的组件进行成员访问  
  如前文所述对成员的访问可以明确指出访问的目标组件，当访问的组件被明确指定后，无论此成员在之后是否有被隐藏，均不会对这个调用产生影响。

  ```ecs
  comp Comp5{
    fun foo(){
      println("hello world")
    }
  }
  
  comp Comp6: Comp5{
    fun print(){
      this:Comp5.foo()
    }
  }
  
  comp Comp7: Comp6{
    fun foo(){
      hidden.foo()
      println("hello EnComping")
    }
  }
  
  new Comp7().print() // 输出 "hello world"
  new Comp7().foo()   // "hello world"
                      // "hello EnComping"
  ```

- 使用符号`hidden`关键字推断指名组件访问  
  在上一个例子中也出现了`hidden`关键字选择，`Comp7`的函数`foo()`中使用了`hidden.foo()`，可推断为指名组件`this:Comp5.foo()`，无论之后是否有被隐藏，均不会对这个调用产生影响。

### 隐藏标记

这是一个与java中`@Override`注解的存在意义相同的标记，因为ECP认为所有的成员只要没有明确告知不可被隐藏就是可被隐藏的，所以对成员的隐藏并没有额外的关键字修饰，但是这可能会使得语义相对混淆。

为此，ECP规定当组件中的成员隐藏了来自其包含的组件中的成员时，应当使用注解`@Over`标记此成员，例如上方的样例代码中的`Comp7`，其规范的形式如下：

```ecs
comp Comp7: Comp6{
  @Over fun foo(){
    hidden.foo()
    println("hello EnComping")
  }
}
```

如上所示，尽管不携带此标记并不会造成致命的错误，但使用此标记可以更加明确语义，同时此注解参与编译检查，在预期此成员会覆盖上级的成员但实际没有覆盖时给出异常提示。
